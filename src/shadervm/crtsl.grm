"Name"     = 'CRTSL'
"Author"   = 'Denys Bulant'
"Version"  = '0.0.01'
"About"    = 'CRT ray tracer shading language grammar'

"Start Symbol" = <definitions>

! ------------------------------------------------- Sets

{ID Head}      = {Letter} + [_]
{ID Tail}      = {Alphanumeric} + [_]
{String Chars} = {Printable} + {HT} - ["]

! ------------------------------------------------- Terminals

Comment Line		= '#'
identifier    	= {ID Head}{ID Tail}*
stringconstant	= '"' {String Chars}* '"'
number 		= {Number}+('.'{Number}+)?
shader_type		= light | surface | volume | displacement | transformation | imager
type			= real |string | vec3 | color

! ------------------------------------------------- Rules

! ------------------------------------- Declarations

<definitions>			::= <shader_definition> <definitions>
				 |  

<shader_definition>		::= shader_type identifier '(' <formals> ')' <block>

<formals>			::= <variable_definitions> <formals>
				 |  

<variables>			::= <variable_definitions> <variables>
				 |  

<variable_definitions>	::= type <def_expressions> ';'

<def_expressions>		::= <def_expression>
				 |  <def_expression> ',' <def_expressions>

<def_expression>		::= identifier <def_init>

<def_init>			::= '=' <add_expr>
				 |  

! ------------------------------------- Statements

<block>			::= '{' <variables> <statements> '}'

<statements>			::= <statements> <statement>
				 |  

<statement>			::= <assignexpression> ';'
				 |  <procedurecall> ';'
				 |  <returnstmt> ';'
				 |  <block>

<returnstmt>			::= 'return' <expression>

! ------------------------------------- Expressions

<expression>			::= <assignexpression>
				 |  <add_expr>

<assignexpression>		::= identifier '=' <add_expr>

<type_ctor>			::= type '(' <proc_arguments> ')'

<procedurecall>		::= identifier '(' <proc_arguments>')'

<proc_arguments>		::= <proc_arguments_list>
				 |  

<proc_arguments_list>	::= <expression> ',' <proc_arguments_list>
				 |  <expression>

<add_expr>			::= <add_expr> '+' <sub_expr>
				 |  <sub_expr>

<sub_expr>			::= <sub_expr> '-' <mult_expr>
				 |  <mult_expr>

<mult_expr>			::= <mult_expr> '*' <dot_expr>
				 |  <dot_expr>

<dot_expr>			::= <dot_expr> '.' <primary>
				 |  <negate>
				 |  <primary>

<negate>			::= '-' <primary>

<primary>			::= number
				 |  identifier
				 |  stringconstant
				 |  <type_ctor>
				 |  <procedurecall>
				 |  '(' <expression> ')'
