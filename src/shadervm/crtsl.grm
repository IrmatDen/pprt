"Name"     = 'CRTSL'
"Author"   = 'Denys Bulant'
"Version"  = '0.0.01'
"About"    = 'CRT ray tracer shading language grammar'

"Start Symbol" = <definitions>

! ------------------------------------------------- Sets

{ID Head}      = {Letter} + [_]
{ID Tail}      = {Alphanumeric} + [_]
{String Chars} = {Printable} + {HT} - ["]

! ------------------------------------------------- Terminals

Comment Line		= '#'
identifier    	= {ID Head}{ID Tail}*
stringconstant	= '"' {String Chars}* '"'
number 		= {Number}+('.'{Number}+)?
shader_type		= light | surface | volume | displacement | transformation | imager
type			= real |string | vec3 | color4

! ------------------------------------------------- Rules

! ------------------------------------- Declarations

<definitions>			::= <shader_definition>

<shader_definition>		::= shader_type identifier '(' <formals> ')' <block>

<formals>			::= <variable_definitions> <formals>
				 |  

<variables>			::= <variable_definitions> <variables>
				 |  

<variable_definitions>	::= type <def_expressions> ';'

<def_expressions>		::= <def_expression>
				 |  <def_expression> ',' <def_expressions>

<def_expression>		::= identifier <def_init>

<def_init>			::= '=' <or_expr>
				 |  

! ------------------------------------- Statements

<block>			::= '{' <variables> <statements> '}'

<statements>			::= <statements> <statement>
				 |  

<statement>			::= <assignexpression> ';'
				 |  <procedurecall> ';'
				 |  <returnstmt> ';'
				 |  <block>

<returnstmt>			::= 'return' <expression>

! ------------------------------------- Expressions

<expression>			::= <assignexpression>
				 |  <or_expr>

<assignexpression>		::= identifier '=' <or_expr>

<type_ctor>			::= type '(' <proc_arguments> ')'

<procedurecall>		::= identifier '(' <proc_arguments> ')'

<proc_arguments>		::= <expression> ',' <proc_arguments>
				 |  <expression>
				 |  

<or_expr>    			::= <or_expr> '||' <and_expr>
				 |  <and_expr>

<and_expr>     		::= <and_expr> '&&' <equ_expr>
				 |  <equ_expr>

<equ_expr>			::= <equ_expr> '==' <comp_expr>
				 |  <equ_expr> '!=' <comp_expr>
				 |  <comp_expr>

<comp_expr>			::= <comp_expr> '<'  <add_expr>
				 |  <comp_expr> '>'  <add_expr>
				 |  <comp_expr> '<=' <add_expr>
				 |  <comp_expr> '>=' <add_expr>
				 |  <add_expr>

<add_expr>			::= <add_expr> '+' <mult_expr>
				 |  <add_expr> '-' <mult_expr>
				 |  <mult_expr>

<mult_expr>			::= <mult_expr> '*' <dot_expr>
				 |  <mult_expr> '/' <dot_expr>
				 |  <dot_expr>

<dot_expr>			::= <dot_expr> '.' <value>
				 |  <value>

<value>			::= number
				 |  identifier
				 |  stringconstant
				 |  <type_ctor>
				 |  <procedurecall>
				 |  '(' <expression> ')'